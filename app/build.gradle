apply plugin: 'com.android.application'

android {
  compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
  buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

  defaultConfig {
    applicationId "cz.expaobserver"
    minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
    targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    // SS-X-YY-ZZZ, S: minSdk, X: major, Y: minor, Z: build
    versionCode 9100003
    versionName "1.0.3"
//    resConfigs "cs", "en"
//    resConfigs "nodpi", "hdpi", "xhdpi", "xxhdpi"
  }

  buildTypes {
    debug {
      versionNameSuffix '-debug'
    }
    release {
//      shrinkResources true
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
    }
  }

  signingConfigs {
    release {
      def keyPropsFile = file('signing.properties')
      if (keyPropsFile.exists()) {
        def Properties keyProps = new Properties()
        keyProps.load(new FileInputStream(keyPropsFile))
        if (keyProps['STORE_FILE'] && keyProps['STORE_PASSWORD'] && keyProps['KEY_ALIAS'] && keyProps['KEY_PASSWORD']) {
          storeFile file(keyProps['STORE_FILE'])
          storePassword keyProps['STORE_PASSWORD']
          keyAlias keyProps['KEY_ALIAS']
          keyPassword keyProps['KEY_PASSWORD']
        } else {
          println 'Incomplete signing.properties file present.'
        }
      } else {
        println 'No signing.properties file present.'
      }
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

dependencies {
  compile 'com.android.support:support-v4:21.+'
  compile 'com.android.support:appcompat-v7:21.+'
//  compile 'com.github.machinarius:preferencefragment:+'
  compile('com.android.support:support-v4-preferencefragment:1.+@aar') {
    exclude module: 'support-v4'
  }
  compile 'com.squareup.okhttp:okhttp:2.+'
  compile 'joda-time:joda-time:2.+'
  compile 'com.jakewharton:butterknife:6.+'
}
